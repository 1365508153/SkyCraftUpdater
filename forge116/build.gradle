buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:4.1.+"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.github.johnrengelman.shadow"

version = "${version_major}.${version_minor}.${version_patch}"
group = "${artifact_group}"
archivesBaseName = "${artifact_basename}-${minecraft_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

minecraft {
    mappings channel: "${mcp_channel}", version: "${mcp_mappings}"

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                updater {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                updater {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                updater {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Spigot maven"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shade project(":common")
}

shadowJar {
    def packages = [
        "org.apache.commons.lang.",
        "org.apache.commons.io.",
        "org.apache.commons.codec.",
        "org.eclipse.jetty.",
        "org.yaml.",
        "javax.servlet.",
        "javax.annotation.",
        "javax.inject.",
        "com.google.gson.",
        "ch.jalu"
    ]
    packages.each { relocate(it, "org.skycraft.updater.shades.${it}") }
    classifier null
    configurations = [project.configurations.shade]
}

reobf {
    shadowJar {}
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "META-INF/mods.toml"

        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "META-INF/mods.toml"
    }
}
